use(require("atomy"))

use(require("anatomy/html"))
html = require("anatomy/renderers/html")

partials = load("concourse/partials")

def(template-for(part, out, done)):
  part css-url-additions << "https://fonts.googleapis.com/css?family=Open+Sans:400,300,600,700"

  part assets << File expand-path("../images/favicon.ico", __FILE__)
  part assets << File expand-path("../fonts", __FILE__)
  part assets << File expand-path("../images/pipes.svg", __FILE__)
  part assets << File expand-path("../css/pipeline-svg.css", __FILE__)
  part assets << File expand-path("../css/splash-pipeline-svg.css", __FILE__)

  -- needed for jQuery
  part js-additions += html search-js-additions
  part js-additions << File expand-path("../js/bootstrap.min.js", __FILE__)

  part css-additions << File expand-path("../css/font-awesome.min.css", __FILE__)
  part css-additions << File expand-path("../css/normalize.css", __FILE__)
  part css-additions << File expand-path("../css/skeleton.css", __FILE__)
  part css-additions << File expand-path("../css/main.css", __FILE__)
  part css-additions << File expand-path("../css/fonts.css", __FILE__)
  part css-additions << File expand-path("../css/highlight.css", __FILE__)
  part css-additions << File expand-path("../css/splash.css", __FILE__)

  part parts each [sub]:
    html render(sub, out, done)

  <div(#everything):
    <div(#header, class = "u-full-width top-intro docs-header"):
      <a(name = "page-top"){}

      partials nav-bar(part)

      <div(class = "white-text txt-c"):
        <div(.row):
          <div(class = "twelve columns"):
            <h1(.intro-slogan):
              <span(.soft-line): "CI that scales with "
              <span(.soft-line): "your project."

      <div(class = "pipes txt-c"):
        <div(.splash-pipeline-image):
          <a(.concourse-pipeline-link, href = "https://ci.concourse.ci", alt = "Concourse Pipeline"){}

          <object(data = "pipes.svg", type = "image/svg+xml", width = "100%", height = "100%"):
            <param(name = "src", value = "pipes.svg")

    <div(#middle):
      html over(part body)

    partials bottom-nav(part)
